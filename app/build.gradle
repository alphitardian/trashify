plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlinx-serialization'
    id 'kotlin-parcelize'
}

def appProperties = new Properties()
def appPropertiesFile = rootProject.file('local.properties')
if (appPropertiesFile.exists()) {
    appProperties.load(appPropertiesFile.newDataInputStream())
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.alphitardian.trashappta"
        minSdk 26
        targetSdk 31
        versionCode 1
        versionName "1.0"

        buildConfigField("String", "MAP_KEY", String.format("\"%s\"", appProperties['MAP_KEY']))
        buildConfigField("String", "IMAGE_API_KEY", String.format("\"%s\"", appProperties['IMAGE_API_KEY']))

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        kotlinCompilerVersion '1.5.21'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    androidResources {
        noCompress 'tflite'
    }
}

dependencies {
// Unit Test
    testImplementation 'junit:junit:4.+'

    // UI Test
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"

    // Android Libs
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.0'
    implementation 'androidx.activity:activity-compose:1.4.0'
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.4.0'
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.0"
    implementation 'androidx.paging:paging-compose:1.0.0-alpha14'
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.0-rc02"
    implementation "androidx.fragment:fragment-ktx:1.4.0"
    implementation 'androidx.core:core-splashscreen:1.0.0-alpha02'

    // Google Map
    implementation 'com.google.maps.android:places-ktx:0.4.0'
    implementation "com.google.android.libraries.maps:maps:3.1.0-beta"
    implementation "com.google.maps.android:maps-v3-ktx:2.2.0"
    implementation 'com.google.maps:google-maps-services:2.0.0'
    implementation 'org.slf4j:slf4j-simple:1.7.25'

    // Navigation Compose
    implementation "androidx.navigation:navigation-compose:$navigation_compose_version"

    // Accompanist
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist_version"
    implementation "com.google.accompanist:accompanist-flowlayout:$accompanist_version"
    implementation "com.google.accompanist:accompanist-placeholder-material:$accompanist_version"
    implementation "com.google.accompanist:accompanist-permissions:$accompanist_version"
    implementation "com.google.accompanist:accompanist-swiperefresh:$accompanist_version"

    // Dagger Hilt
    kapt "com.google.dagger:hilt-android-compiler:2.38.1"
    kapt "androidx.hilt:hilt-compiler:1.0.0"
    implementation "com.google.dagger:hilt-android:2.39"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0-alpha03'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'

    // Image Loader
    implementation "com.github.skydoves:landscapist-glide:1.4.5"

    // Datastore
    implementation "androidx.datastore:datastore:1.0.0"
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // Room DB
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // ML Kit
    implementation 'com.google.mlkit:image-labeling-custom:17.0.0'

    // JWT Decoder
    implementation 'com.auth0.android:jwtdecode:2.0.1'

    // Lottie Airbnb Animation
    implementation "com.airbnb.android:lottie-compose:4.2.2"
}